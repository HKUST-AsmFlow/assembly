{
    parserClass = "io.github.asmflow.assembly.armv7.parser.ARMv7Parser"
    parserUtilClass = "io.github.asmflow.assembly.armv7.parser.ARMv7ParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    elementTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7ElementType"
    elementTypeHolderClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenTypes"

    psiClassPrefix = "ARMv7"
    psiPackage = "io.github.asmflow.assembly.armv7.psi"
    psiImplClassSuffix = "Impl"
    psiImplPackage = "io.github.asmflow.assembly.armv7.psi.impl"

    tokenTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenType"

    classHeader = "header.txt"

    tokens = [
        COMMENT = "comment"
        IDENTIFIER = "identifier"
        LINE_FEED = "lf"
        STRING = "string"

        // numbers
        BINARY_NUMBER = "binary number"
        DECIMAL_NUMBER = "decimal number"
        HEXADECIMAL_NUMBER = "hexadecimal number"
        OCTAL_NUMBER = "octal number"

        // directives
        ALIGN = "align"
        ASCII = "ascii"
        ASCIZ = "asciz"
        BALIGN = "balign"
        BALIGNL = "balignl"
        BALIGNW = "balignw"
        BYTE = "byte"
        COMM = "comm"
        DATA = "data"
        DEF = "def"
        DESC = "desc"
        DIM = "dim"
        DOUBLE = "double"
        EJECT = "eject"
        ELSE = "else"
        ENDEF = "endef"
        ENDIF = "endif"
        EQU = "equ"
        EQUIV = "equiv"
        ERR = "err"
        EXTERN = "extern"
        FILE = "file"
        FILL = "fill"
        FLOAT = "float"
        GLOBAL = "global"
        HWORD = "hword"
        IDENT = "ident"
        IF = "if"
        INCLUDE = "include"
        INT = "int"
        IRP = "irp"
        IRPC = "irpc"
        LCOMM = "lcomm"
        LFLAGS = "lflags"
        LINKONCE = "linkonce"
        LN = "ln"
        LIST = "list"
        LONG = "long"
        MACRO = "macro"
        NOLIST = "nolist"
        OCTA = "octa"
        ORG = "org"
        P2ALIGN = "p2align"
        P2ALIGNL = "p2alignl"
        P2ALIGNW = "p2alignw"
        PSIZE = "psize"
        QUAD = "quad"
        REPT = "rept"
        SBTTL = "sbttl"
        SCL = "scl"
        SECTION = "section"
        SET = "set"
        SHORT = "short"
        SINGLE = "single"
        SIZE = "size"
        SLEB128 = "sleb128"
        SKIP = "skip"
        SPACE = "space"
        STABD = "stabd"
        STABN = "stabn"
        STABS = "stabs"
        STRING = "string"
        SYMVER = "symver"
        TAG = "tag"
        TEXT = "text"
        TITLE = "title"
        TYPE = "type"
        VAR = "var"
        ULEB128 = "uleb128"
        WORD = "word"

        // instructions
        ADC = "adc"
        ADD = "add"
        ADR = "adr"
        AND = "and"
        ASR = "asr"
        B = "b"
        BFC = "bfc"
        BFI = "bfi"
        BIC = "bic"
        BKPT = "bkpt"
        BL = "bl"
        BX = "bx"
        BXJ = "bxj"
        CBNZ = "cbnz"
        CBZ = "cbz"
        CLREX = "clrex"
        CLZ = "clz"
        CMN = "cmn"
        CMP = "cmp"
        CSDB = "csdb"
        DBG = "dbg"
        DMB = "dmb"
        DSB = "dsb"
        EOR = "eor"
        ERET = "eret"
        HVC = "hvc"
        ISB = "isb"
        LDM = "ldm"
        LDMIA = "ldmia"
        LDMFD = "ldmfd"
        LDMDA = "ldmda"
        LDMFA = "ldmfa"
        LDMDB = "ldmdb"
        LDMEA = "ldmea"
        LDMIB = "ldmib"
        LDMED = "ldmeb"

        // registers
        R0 = "r0"
        R1 = "r1"
        R2 = "r2"
        R3 = "r3"
        R4 = "r4"
        R5 = "r5"
        R6 = "r6"
        R7 = "r7"
        R8 = "r8"
        R9 = "r9"
        R10 = "r10"
        R11 = "r11"
        R12 = "r12"
        SP = "sp"
        LR = "lr"
        PC = "PC"
        CPSR = "CPSR"
        SPSR = "SPSR"

        // shift type
        LSL = "lsl"
        LSR = "lsr"
        ASR = "asr"
        ROR = "ror"
        RRX = "rrx"

        // symbols
        COMMA = ","
        COLON = ":"
        DOT = "."
        LBRACKET = "["
        MINUS = "-"
        PLUS = "+"
        POUND = "#"
        RBRACKET = "]"
    ]

    mixin(".+Instruction") = "io.github.asmflow.assembly.armv7.psi.impl.ARMv7InstructionSuffixImpl"
    implements(".+Instruction") = "io.github.asmflow.assembly.armv7.psi.IARMv7InstructionSuffix"
}

File ::= Item*

private LineFeed ::= LINE_FEED+ {
    consumeTokenMethod = "consumeTokenFast"
}

private DirectiveOrInstructionOrLabel ::= AssemblerDirective | Instruction | Label

private AssemblerDirective ::= ARMv7Directives
private Instruction ::= ARMv7ArithmeticInstructions
    | ARMv7BranchInstructions
    | ARMv7ComparisonInstructions
    | ARMv7DebugInstructions
    | ARMv7LoadInstructions
    | ARMv7LockInstructions
    | ARMv7SpeculativeInstructions
    | ARmv7SystemInstructions

Label ::= IDENTIFIER COLON

private Item ::= LineFeed | DirectiveOrInstructionOrLabel LineFeed?

Based ::= !<<afterWhitespace>> (BINARY_NUMBER | DECIMAL_NUMBER | HEXADECIMAL_NUMBER | OCTAL_NUMBER)
Number ::= POUND (PLUS | MINUS)? Based
Numbers ::= <<CommaSeparatedList Number>>

Register ::= R0 | R1 | R2 | R3 | R4 | R5 | R6 | R7 | R8 | R9| R10 | R11 | R12 | SP | LR | PC | CPSR | SPSR {
  mixin = "io.github.asmflow.assembly.armv7.psi.impl.ARMv7RegisterSuffixImpl"
  implements = "io.github.asmflow.assembly.armv7.psi.IARMv7RegisterSuffix"
}
private RegisterWithOffset ::= PostindexOffset | NoOffset | PreindexOffset
NoOffset ::= LBRACKET Register RBRACKET
PreindexOffset ::= LBRACKET Register COMMA Number RBRACKET
PostindexOffset ::= LBRACKET Register RBRACKET COMMA Number
Registers ::= <<CommaSeparatedList Register>>

ShiftType ::= ASR | LSL | LSR | ROR | RRX
Shift ::= ShiftType (Number | Register)

private ARMv7ArithmeticInstructions ::= AdcInstruction
    | AddInstruction
    | AdrInstruction
    | AndInstruction
    | AsrInstruction
    | BfcInstruction
    | BfiInstruction
    | BicInstruction
    | ClzInstruction
    | EorInstruction

private ARMv7BranchInstructions ::= BInstruction
    | BlInstruction
    | BxInstruction
    | BxjInstruction
    | CbnzInstruction
    | CbzInstruction

private ARMv7ComparisonInstructions ::= CmnInstruction
    | CmpInstruction

private ARMv7DebugInstructions ::= BkptInstruction
    | DbgInstruction

private ARMv7LoadInstructions ::= LdmInstruction
    | LdmfdPseudoInstruction
    | LdmiaPseudoInstruction
    | LdmdaInstruction
    | LdmfaPseudoInstruction
    | LdmdbInstruction
    | LdmeaPseudoInstruction
    | LdmibInstruction
    | LdmedPseudoInstruction
    | LdrInstruction

private ARMv7LockInstructions ::= ClrexInstruction

private ARMv7SpeculativeInstructions ::= CsdbInstruction
    | DmbInstruction
    | DsbInstruction
    | IsbInstruction

private ARmv7SystemInstructions ::= EretInstruction
    | HvcInstruction

AdcInstruction ::= ADC Registers (COMMA (Number | Shift))?
AddInstruction ::= ADD Registers (COMMA (Number | Shift))?
AdrInstruction ::= ADR Register COMMA IDENTIFIER
AndInstruction ::= AND Registers (COMMA (Number | Shift))?
AsrInstruction ::= ASR Registers (COMMA Number)?

BInstruction ::= B IDENTIFIER
BfcInstruction ::= BFC Register COMMA Numbers
BfiInstruction ::= BFI Registers COMMA Numbers
BicInstruction ::= BIC Registers (COMMA (Number | Shift))?
BkptInstruction ::= BKPT Number
BlInstruction ::= BL IDENTIFIER
BxInstruction ::= BX Register
BxjInstruction ::= BXJ Register

CbnzInstruction ::= CBNZ Register COMMA IDENTIFIER
CbzInstruction ::= CBZ Register COMMA IDENTIFIER
ClrexInstruction ::= CLREX
ClzInstruction ::= CLZ Register COMMA Register
CmnInstruction ::= CMN Register COMMA (Number | Register (COMMA Shift)?)
CmpInstruction ::= CMP Register COMMA (Number | Register (COMMA Shift)?)
CsdbInstruction ::= CSDB

DbgInstruction ::= DBG Number
DmbInstruction ::= DMB IDENTIFIER
DsbInstruction ::= DSB IDENTIFIER

EorInstruction ::= EOR Registers (COMMA (Number | Shift))?
EretInstruction ::= ERET

HvcInstruction ::= HVC Number

IsbInstruction ::= ISB IDENTIFIER

LdmInstruction ::= LDM Register COMMA Registers
LdmfdPseudoInstruction ::= LDMFD Register COMMA Registers
LdmiaPseudoInstruction ::= LDMIA Register COMMA Registers
LdmdaInstruction ::= LDMDA Register COMMA Registers
LdmfaPseudoInstruction ::= LDMFA Register COMMA Registers
LdmdbInstruction ::= LDMDB Register COMMA Registers
LdmeaPseudoInstruction ::= LDMEA Register COMMA Registers
LdmibInstruction ::= LDMIB Register COMMA Registers
LdmedPseudoInstruction ::= LDMED Register COMMA Registers
LdrInstruction ::= LDR Register COMMA RegisterWithOffset

private ARMv7Directives ::= AlignDirective | AsciiDirective | GlobalDirective

AlignDirective ::= DOT ALIGN <<CommaSeparatedList Based>>
AsciiDirective ::= DOT ASCII <<CommaSeparatedList STRING>>
GlobalDirective ::= DOT GLOBAL IDENTIFIER {
  mixin = "io.github.asmflow.assembly.armv7.psi.impl.ARMv7GlobalDirectiveMixinImpl"
  implements = "io.github.asmflow.assembly.armv7.psi.IARMv7GlobalDirectiveMixin"
}

private meta CommaSeparatedList ::= <<T>> (COMMA <<T>>)*

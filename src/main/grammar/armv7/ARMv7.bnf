{
    parserClass = "io.github.asmflow.assembly.armv7.parser.ARMv7Parser"
    parserUtilClass = "io.github.asmflow.assembly.armv7.parser.ARMv7ParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    elementTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7ElementType"
    elementTypeHolderClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenTypes"

    psiClassPrefix = "ARMv7"
    psiPackage = "io.github.asmflow.assembly.armv7.psi"
    psiImplClassSuffix = "Impl"
    psiImplPackage = "io.github.asmflow.assembly.armv7.psi.impl"

    tokenTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenType"

    classHeader = "header.txt"

    tokens = [
        COMMENT = "comment"
        IDENTIFIER = "identifier"
        LINE_FEED = "lf"
        STRING = "string"

        // numbers
        BINARY_NUMBER = "binary number"
        DECIMAL_NUMBER = "decimal number"
        HEXADECIMAL_NUMBER = "hexadecimal number"
        OCTAL_NUMBER = "octal number"

        // registers
        R0 = "r0"
        R1 = "r1"
        R2 = "r2"
        R3 = "r3"
        R4 = "r4"
        R5 = "r5"
        R6 = "r6"
        R7 = "r7"
        R8 = "r8"
        R9 = "r9"
        R10 = "r10"
        R11 = "r11"
        R12 = "r12"
        SP = "sp"
        LR = "lr"
        PC = "PC"
        CPSR = "CPSR"
        SPSR = "SPSR"

        // symbols
        BANG = "!"
        COMMA = ","
        COLON = ":"
        DOT = "."
        LBRACKET = "["
        MINUS = "-"
        PLUS = "+"
        POUND = "#"
        RBRACKET = "]"
    ]
}

File ::= Item*
private Item ::= LineFeed | (LabelWithColon | Directive | Instruction) LineFeed?

private LineFeed ::= LINE_FEED+ {
    consumeTokenMethod = "consumeTokenFast"
}

Register ::= R0 | R1 | R2 | R3 | R4 | R5 | R6 | R7 | R8 | R9 | R10 | R11 | R12 | SP | LR | PC | CPSR | SPSR

Directive ::= DOT DirectiveName DirectiveParameters?
DirectiveName ::= IDENTIFIER
DirectiveParameters ::= <<CommaSeparatedList DirectiveParameter>>
DirectiveParameter ::= Label | STRING

Instruction ::= Mnemonic Operands? {
  mixin = "io.github.asmflow.assembly.armv7.psi.impl.ARMv7InstructionMixinImpl"
  implements = "io.github.asmflow.assembly.armv7.psi.ARMv7InstructionMixin"
}
Operands ::= <<CommaSeparatedList Operand>>
Operand ::= RegisterOperand | Label | Number

private RegisterOperand ::= RegisterWithShift
  | Postindexed
  | Offset
  | OffsetVariant
  | Preindexed

RegisterWithShift ::= Register (COMMA ShiftType (Number | Register))?
Offset ::= LBRACKET Register (COMMA Number)? RBRACKET
OffsetVariant ::= LBRACKET Register COMMA RegisterWithShift RBRACKET BANG?
Preindexed ::= LBRACKET Register COMMA Number RBRACKET BANG
Postindexed ::= LBRACKET Register RBRACKET COMMA Number

Mnemonic ::= IDENTIFIER

ShiftType ::= IDENTIFIER

LabelWithColon ::= Label COLON
Label ::= IDENTIFIER

Number ::= POUND (PLUS | MINUS)? Based
private Based ::= !<<afterWhitespace>> (BINARY_NUMBER | DECIMAL_NUMBER | HEXADECIMAL_NUMBER | OCTAL_NUMBER)

private meta CommaSeparatedList ::= <<T>> (COMMA <<T>>)*

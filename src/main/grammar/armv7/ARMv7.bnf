{
    parserClass = "io.github.asmflow.assembly.armv7.parser.ARMv7Parser"
    parserUtilClass = "io.github.asmflow.assembly.armv7.parser.ARMv7ParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    elementTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7ElementType"
    elementTypeHolderClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenTypes"

    psiClassPrefix = "ARMv7"
    psiPackage = "io.github.asmflow.assembly.armv7.psi"
    psiImplClassSuffix = "Impl"
    psiImplPackage = "io.github.asmflow.assembly.armv7.psi.impl"

    tokenTypeClass = "io.github.asmflow.assembly.armv7.psi.ARMv7TokenType"

    classHeader = "header.txt"

    tokens = [
        COMMENT = "comment"
        IDENTIFIER = "identifier"
        LINE_FEED = "lf"
        STRING = "string"

        // numbers
        BINARY_NUMBER = "binary number"
        DECIMAL_NUMBER = "decimal number"
        HEXADECIMAL_NUMBER = "hexadecimal number"
        OCTAL_NUMBER = "octal number"

        // set condition flags (optional)
        S = "s"

        // condition codes (optional)
        EQ = "eq"
        NE = "ne"
        CS = "cs"
        HS = "hs"
        CC = "cc"
        LO = "lo"
        MI = "mi"
        PL = "pl"
        VS = "vs"
        VC = "vc"
        HI = "hi"
        LS = "ls"
        GE = "ge"
        LT = "lt"
        GT = "gt"
        LE = "le"
        AL = "al"

        // directives
        ALIGN = "align"
        ASCII = "ascii"
        ASCIZ = "asciz"
        BALIGN = "balign"
        BALIGNL = "balignl"
        BALIGNW = "balignw"
        BYTE = "byte"
        COMM = "comm"
        DATA = "data"
        DEF = "def"
        DESC = "desc"
        DIM = "dim"
        DOUBLE = "double"
        EJECT = "eject"
        ELSE = "else"
        ENDEF = "endef"
        ENDIF = "endif"
        EQU = "equ"
        EQUIV = "equiv"
        ERR = "err"
        EXTERN = "extern"
        FILE = "file"
        FILL = "fill"
        FLOAT = "float"
        GLOBAL = "global"
        HWORD = "hword"
        IDENT = "ident"
        IF = "if"
        INCLUDE = "include"
        INT = "int"
        IRP = "irp"
        IRPC = "irpc"
        LCOMM = "lcomm"
        LFLAGS = "lflags"
        LINKONCE = "linkonce"
        LN = "ln"
        LIST = "list"
        LONG = "long"
        MACRO = "macro"
        NOLIST = "nolist"
        OCTA = "octa"
        ORG = "org"
        P2ALIGN = "p2align"
        P2ALIGNL = "p2alignl"
        P2ALIGNW = "p2alignw"
        PSIZE = "psize"
        QUAD = "quad"
        REPT = "rept"
        SBTTL = "sbttl"
        SCL = "scl"
        SECTION = "section"
        SET = "set"
        SHORT = "short"
        SINGLE = "single"
        SIZE = "size"
        SLEB128 = "sleb128"
        SKIP = "skip"
        SPACE = "space"
        STABD = "stabd"
        STABN = "stabn"
        STABS = "stabs"
        STRING = "string"
        SYMVER = "symver"
        TAG = "tag"
        TEXT = "text"
        TITLE = "title"
        TYPE = "type"
        VAR = "var"
        ULEB128 = "uleb128"
        WORD = "word"

        // instructions
        ADC = "adc"
        ADD = "add"
        ADR = "adr"
        AND = "and"
        ASR = "asr"

        B = "b"
        BFC = "bfc"
        BFI = "bfi"

        // registers
        R0 = "r0"
        R1 = "r1"
        R2 = "r2"
        R3 = "r3"
        R4 = "r4"
        R5 = "r5"
        R6 = "r6"
        R7 = "r7"
        R8 = "r8"
        R9 = "r9"
        R10 = "r10"
        R11 = "r11"
        R12 = "r12"
        SP = "sp"
        LR = "lr"
        PC = "PC"
        CPSR = "CPSR"
        SPSR = "SPSR"

        // shift type
        LSL = "lsl"
        LSR = "lsr"
        ASR = "asr"
        ROR = "ror"
        RRX = "rrx"

        // symbols
        COMMA = ","
        COLON = ":"
        DOT = "."
        POUND = "#"
    ]
}

File ::= Item*

private LineFeed ::= LINE_FEED+ {
    consumeTokenMethod = "consumeTokenFast"
}

private DirectiveOrInstructionOrLabel ::= Directive | Instruction | Label

private Directive ::= DOT ARMv7Directives
private Instruction ::= ARMv7ArithmeticInstructions | ARMv7BranchInstructions

Label ::= IDENTIFIER COLON

Item ::= LineFeed | DirectiveOrInstructionOrLabel LineFeed?

Based ::= !<<afterWhitespace>> (BINARY_NUMBER | DECIMAL_NUMBER | HEXADECIMAL_NUMBER | OCTAL_NUMBER)
Number ::= POUND Based
Numbers ::= <<CommaSeparatedList Number>>

ConditionCodes ::= !<<afterWhitespace>> (EQ | NE | CS | HS | CC | LO | MI | PL | VS | VC | HI | LS | GE | LT | GT | LE | AL)

Register ::= R0 | R1 | R2 | R3 | R4 | R5 | R6 | R7 | R8 | R9| R10 | R11 | R12 | SP | LR | PC | CPSR | SPSR
Registers ::= <<CommaSeparatedList Register>>

SetConditionFlags ::= !<<afterWhitespace>> (S)

ShiftType ::= ASR | LSL | LSR | ROR | RRX
Shift ::= ShiftType (Number | Register)

private ARMv7ArithmeticInstructions ::= AdcInstruction
    | AddInstruction
    | AdrInstruction
    | AndInstruction
    | AsrInstruction
    | BfcInstruction
    | BfiInstruction

private ARMv7BranchInstructions ::= BInstruction

AdcInstruction ::= ADC SetConditionFlags? ConditionCodes? Registers (COMMA (Number | Shift))?
AddInstruction ::= ADD SetConditionFlags? ConditionCodes? Registers (COMMA (Number | Shift))?
AdrInstruction ::= ADR ConditionCodes? Register COMMA IDENTIFIER
AndInstruction ::= AND SetConditionFlags? ConditionCodes? Registers (COMMA (Number | Shift))?
AsrInstruction ::= ASR SetConditionFlags? ConditionCodes? Registers (COMMA Number)?

BInstruction ::= B ConditionCodes? IDENTIFIER
BfcInstruction ::= BFC ConditionCodes? Register COMMA Numbers
BfiInstruction ::= BFI ConditionCodes? Registers COMMA Numbers

private ARMv7Directives ::= AlignDirective | AsciiDirective | GlobalDirective

AlignDirective ::= ALIGN <<CommaSeparatedList Based>>
AsciiDirective ::= ASCII <<CommaSeparatedList STRING>>
GlobalDirective ::= GLOBAL IDENTIFIER

private meta CommaSeparatedList ::= <<T>> (COMMA <<T>>)*
